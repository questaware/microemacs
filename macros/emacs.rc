set $discmd 0
set $col1ch 1
set $col2ch 8
set $palette 1234560
set $highpat1 4target
;set $pagelen 74

add-global-mode re
add-global-mode GREY
add-global-mode black
add-mode GREY
add-mode black


1 store-macro		; change case one character
  set %ccc $curchar
  !if &gre %ccc 96 
    set $curchar &sub %ccc 32
  !else
    !if &and &gre %ccc 64 &les %ccc 91
        set $curchar &add %ccc 32
      !endif
    !endif
  !endif
  forward-character
!endm    

btk execute-macro-1 

2 store-macro		; end buffer
 	!if &equ $lastkey 47
	  save-file
  !endif

	set %wd $winnew
	set %wk $work
  set %mybuf $cbufname
  set $status 0
	!force drop-buffer %mybuf
	!if &not &seq $status "TRUE"
		select-buffer %mybuf
	!else
		!if &equ $work %wk
  		!force exit-emacs
	  !endif
	!endif
	!if &or &equ $work 1 &equ %wd 1
		set $discmd &sub $discmd 1
		!force delete-window
		set $discmd &add $discmd 1
	!endif
!endm

btk execute-macro-2 M-\
btk execute-macro-2 M-`
btk execute-macro-2 M-/

3 store-macro
	set %ch &chr $curchar

	!while &or &not &seq &low %ch &upp %ch &seq &xla %ch "0123456789" "__________" "_"
		forward-character
		set %ch &chr $curchar
	!endw
!endm

btk execute-macro-3 ^@

4 store-macro		; new file
  set %fle @&cat &cat "new file [" $cfname "]: "
  !if &equ 0 &length %fle
     set %ln $curline
     read-file $cfname
     goto-line %ln
  !else
     set %bnm $cbufname
     find-file %fle
     !if &not &seq %bnm $cbufname
       drop-buffer %bnm
     !endif
  !endif
!endm

btk execute-macro-4 ^X

5 store-macro		; search in the last direction
  !if &les $lastdir 0
    hunt-backward
  !else
    hunt-forward
  !endif  
!endm  

btk execute-macro-5 FN1
btk execute-macro-5 ^N

6 store-macro		; toggle msb one character
  set $curchar &bxor $curchar 128
  forward-character
!endm    

btk execute-macro-6 ^^

7 store-macro    ; construct a number sequence
    search-forward NN
    delete-next-character
    delete-next-character
    set %nn &add %nn 1
    insert-string %nn
!endm 

btk execute-macro-7 FN4

8 store-macro		; copy previous line 
  set %ca &dit
  insert-string %ca
!endm  

btk execute-macro-8 FN^C
btk execute-macro-8 ^_
btk execute-macro-8 ^Z

9 store-macro		; copy line
  beginning-of-line
  set-mark
1 next-line
  copy-region
  set $discmd FALSE
  remove-mark
  set $discmd TRUE
!endm

btk execute-macro-9 ^C 
btk execute-macro-9 A-C

10 store-macro 		; do line
; set %oo @&cat "key " $lastkey
  !if &equ $lastkey 2098
    next-line
  !endif
  execute-macro
  !if &gre $cwline 20
    3 move-window-down
  !endif
!endm

btk end-macro A-FN^\
btk execute-macro-10 FN2
btk execute-macro-10 FN^2

11 store-macro		; kill line
  !if &equ $lastkey 260
	  beginning-of-line
		1 kill-to-end-of-line
	!else
		!while &equ 1 1
			backward-character
			!if &and &not &equ $curchar 32 &not &equ $curchar 9
				!break
			!endif
			delete-next-character
		!endw
		forward-character
	!endif
!endm

btk execute-macro-11 ^D
btk execute-macro-11 M-^H

12 store-macro		; User defined word movement
	set %lk $lastkey
	set %clamp 50
	set %inspace &xla $curchar " 	" "  " " "
	set %class -1
	set %stage 0

	!while &gre %clamp 0
		set %clamp &sub %clamp 1
		set %ch &low &chr $curchar
		set %x &xla %ch " 	abcdefghijklmnopqrstuvwxyz0123456789" "  ____________________________________" 
		!if &les %class 0
			set %class %x
		!else
			!if &not &seq %x %class
				!break
			!endif
		!endif
		!if &not &equ %lk 2362
			backward-character
		!else
			forward-character
		!endif
	!endw
	!if &and &not &equ %lk 2362 &les %clamp 48
		forward-character
	!endif
!endm

btk execute-macro-12 FN^0
btk execute-macro-12 FN^:

13 store-macro         ; expand macros
  !if &equ $uarg 0
    execute-file error.rc
  !else
    execute-file diffs.rc
  !endif  
!endm

btk execute-macro-13 M-I

14 store-macro	 	; yank, clear kill buffer
  yank
  kill-kill
!endm

btk execute-macro-14 FNC
btk yank M-FNC

15 store-macro
  set %scp "pscp -P 2202 -pw "
;  insert-string "F	"
!endm

btk execute-macro-15 A-Q

16 store-macro		; eow, bol or bof
  set-mark
  next-word
0 backward-word
  !if &equ $uarg 0
    copy-region
  !else
    kill-region
  !endif
!endm

btk execute-macro-16 M-FND

btk beginning-of-line FN<
btk search-incls     M-FN<

;btk end-of-line FN>

btk beginning-of-file M-FNZ
btk end-of-file M-FNV
btk beginning-of-file A-M-^Z
btk end-of-file A-M-(

btk beginning-of-line FN9
btk backward-word FN0
btk next-word FN:
btk end-of-line FN;

17 store-macro       ; 
  !if &equ $keycount 1	; sequence of keystrokes
    search-forward "[A-Za-z0-9_][A-Za-z0-9_]*"
    set %mtch $match
  !endif
  !if &or &equ $lastkey 2384 &equ $lastkey 18498
;	   set %oo @&cat "sr" %mtch
     search-reverse %mtch
  !else
   2 search-forward %mtch
  !endif
!endm

btk execute-macro-17 A-FNB
btk execute-macro-17 A-FNF
btk execute-macro-17 M-FNB
btk execute-macro-17 M-FNF

18 store-macro
   insert-string "{ "
   newline
   insert-string "}"
   backward-line
   end-of-line
!endm

btk execute-macro-18 M-^]	; CTRL {}

19 store-macro ; do-os-cmd
; set %oo @&cat "doc " $uarg
  !if &seq %doosf "ERROR"
     set %doosf "ll "
     set %doosfe " -//.c -v ."
  !endif

  set %oo @&cat &cat %doosf $search %doosfe
	pipe-command %oo
!endm

btk execute-macro-19 ^O

20 store-macro ; get pw
  end-of-line
  search-reverse "pw:"
  3 forward-character
	!if &les $curchar 33
		forward-character
	!endif
	set-mark
  end-of-line
	copy-region
	beginning-of-line
!endm

btk execute-macro-20 ^P

21 store-macro ; test
	set %cmd &cat &cat "git diff --no-index -- " $cfname " -"
	-1 pipe-command %cmd
!endm

btk execute-macro-21 ^XV

22 store-macro ; buffhook
	set $highpat2 ""
	set $highpat3 ""
	set $highpat4 ""
	!if &seq &rig $cbufname 3 ".py"
		set $highpat2 "4 def "
    set $highpat3 "4 if "
		set $highpat4 "4else:"
	!endif
!endm

set $exbhook execute-macro-22

btk search-forward A-S
btk save-file ^XA-S

btk select-buffer ^XB
btk next-buffer M-FN;
btk last-buffer M-FN9
btk move-window-down A-FNN
btk move-window-up   A-FNP
btk next-window     M-FNN
btk previous-window M-FNP
btk find-tag M-FN>

btk help A-1               ; SNA like
;btk split-current-window A-2
btk exit-emacs A-3
;A-4 A-5 not known
btk shell-command A-6
btk backward-page A-^Z
btk next-page A-(

btk pipe-command ^X'

set $discmd 1

set %istrl 64

set $hardtab 2
set $fileprof ".c=c2.cpp=c2.cxx=c2.cs=c2.h=c2.hpp=c2.pc=c2.jav=c2.prl=p2.pl=p2.for=f2.fre=f2.inc=f2.pre=f2.f=f2.sql=^q4.pas=P2.md=m2.py=p-2"
